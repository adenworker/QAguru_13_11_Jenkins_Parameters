plugins {
    id 'java'
    id 'io.qameta.allure' version '2.10.0'
}

group 'guru.qa'
version '1.0'

repositories {
    mavenCentral()
}

def allureVersion = "2.18.1",
    selenideVersion = "6.6.4",
    junitVersion = "5.8.2",
    slf4jVersion = "1.7.32",
    ownerVersion = "1.0.12"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation (
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion"
    )
}

test {
    //systemProperties(System.getProperties()) позволяет брать параметры из командной строки при запуске тестов этого сьюта
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
//Таска, которую можно передать гредлу при запуске через командную строку
//Содержит тэги нужных тестов и (опционально) теги ненужных
task tagGradleJenkins(type: Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform{
        includeTags "needed"
        excludeTags "needless"
    }
}